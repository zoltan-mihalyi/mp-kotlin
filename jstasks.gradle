
apply plugin: 'com.moowork.node'

[compileKotlin2Js, compileTestKotlin2Js]*.configure {
    kotlinOptions.moduleKind = "commonjs"
}

task populateNodeModules(type: Copy, dependsOn: compileKotlin2Js) {
    from compileKotlin2Js.destinationDir

    configurations.testCompile.each {
        from zipTree(it.absolutePath).matching { include '*.js' }
    }

    into "${buildDir}/node_modules"
}

task npmInstallMocha(type: NpmTask){
    args = ['install', 'mocha']
}

task runJs(type: NodeTask, dependsOn: [compileKotlin2Js, populateNodeModules, npmInstall]){
    script = compileKotlin2Js.outputFile
}

task runMocha(type: NodeTask, dependsOn: [compileTestKotlin2Js, populateNodeModules, npmInstallMocha]) {
    script = file('node_modules/mocha/bin/mocha')
    args = [compileTestKotlin2Js.outputFile]
}

test.dependsOn runMocha